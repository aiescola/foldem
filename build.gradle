
allprojects {
	repositories {
		jcenter()
	}
	apply plugin: 'maven'
	apply plugin: 'maven-publish'
	apply plugin: 'java'
}

dependencies {
	testCompile 'junit:junit:4.12'
}

buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
	}

}

apply plugin: 'jacoco'
jacocoTestReport {
	reports {
		xml.enabled true
		html.enabled false
	}
}


apply plugin: 'com.jfrog.bintray'

bintray {
	user = project.hasProperty('btUser') ? project.property('btUser') : ''
	key = project.hasProperty('btKey') ? project.property('btKey') : ''
	pkg {
		repo = 'pub'	
		name = 'foldem'
		licenses = ['GPL-3.0']
		vcsUrl = 'https://github.com/derive/foldem.git'
		version {
			gpg {
				sign = true
				passphrase = project.hasProperty('signing.password') ? project.property('signing.password') : ''
			}
			mavenCentralSync {
				sync = true
				user = project.hasProperty('mvnSyncUser') ? project.property('mvnSyncUser') : ''
				password = project.hasProperty('mvnSyncPassword') ? project.property('mvnSyncPassword') : ''
			}		 
			name = '1.0.2'
			desc = 'A Texas Hold \'em Poker Analysis Framework for Java.'
		}
	}
	publications = ['FoldemPublication'] 
}

def pomConfig = {
	licenses {
		license {
			name 'GNU General Public License v3.0'
			url 'https://opensource.org/licenses/GPL-3.0'
			distribution 'repo'
		}
	}
	developers {
		developer {
			id 'derive'
			name 'Derive Aliasington'
			email 'mail@derive.codes'
		}
	}
	scm {
   		url 'https://github.com/ableiten/foldem'
		connection 'https://github.com/ableiten/foldem'
		developerConnection 'https://github.com/ableiten/foldem'
	}
}


task sourceJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

publishing {
	publications {
		FoldemPublication(MavenPublication) {
			from components.java
			groupId 'codes.derive'
			artifactId 'foldem'
			version '1.0.2'
			pom.withXml {
				def root = asNode()
				root.appendNode('description', 'A Texas Hold \'em Poker Analysis Framework for Java.')
				root.appendNode('name', 'Fold\'em')
				root.appendNode('url', 'https://github.com/ableiten/foldem')
				root.children().last() + pomConfig
			}
			artifact sourceJar 
			artifact javadocJar
		}
	}
}
